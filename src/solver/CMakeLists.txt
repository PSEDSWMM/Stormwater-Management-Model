#
# CMakeLists.txt - CMake configuration file for swmm-solver/library
#
# Created: Jul 11, 2019
# Updated: Oct 30, 2024
#
# Author: Michael E. Tryby
#         US EPA ORD/CESER
#

if(APPLE)
    include(${CMAKE_CURRENT_SOURCE_DIR}/../../extern/openmp.cmake)
endif()

find_package(
  OpenMP
    COMPONENTS
        C REQUIRED
)


# configure file groups
set(SWMM_PUBLIC_HEADERS
    include/swmm5.h
)

file(GLOB
    SWMM_SOURCES
        RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} *.c *.h
)

if(BUILD_DEF)
    # Build library with def file interface for backward compatibility
    set_source_files_properties(
        ${PROJECT_SOURCE_DIR}/bindings/swmm5.def
            PROPERTIES_HEADER_FILE_ONLY
                TRUE
    )

    add_library(
      swmm5
        SHARED
            ${SWMM_SOURCES}
            ${PROJECT_SOURCE_DIR}/bindings/swmm5.def
    )

else()

    add_library(
      swmm5
        SHARED
            ${SWMM_SOURCES}
    )

endif()

target_compile_options(
  swmm5
    PUBLIC
        $<$<C_COMPILER_ID:MSVC>:
            $<$<CONFIG:Release>:/GL>
            $<$<CONFIG:Release>:/fp:fast>
            $<$<CONFIG:Release>:/Zi>
        >
)

target_link_options(
  swmm5
    PUBLIC
        $<$<C_COMPILER_ID:MSVC>:
            $<$<CONFIG:Release>:/LTCG:incremental>
        >
)

target_link_libraries(
  swmm5
    PRIVATE
        $<$<NOT:$<BOOL:$<C_COMPILER_ID:MSVC>>>:m>
        $<$<BOOL:OpenMP_C_FOUND>:OpenMP::OpenMP_C>
)

target_include_directories(
  swmm5
    PUBLIC
        $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
        $<INSTALL_INTERFACE:${INCLUDE_DIST}>
)

install(
    TARGETS
        swmm5 omp
    EXPORT
        swmm5Targets
    RUNTIME
        DESTINATION
            ${CMAKE_INSTALL_BINDIR}
    LIBRARY
        DESTINATION
           ${CMAKE_INSTALL_LIBDIR}
    ARCHIVE
        DESTINATION
            ${CMAKE_INSTALL_LIBDIR}
)

# Create target import scripts so other cmake projects can use swmm libraries
install(
    EXPORT
        swmm5Targets
    DESTINATION
        ${CONFIG_DIST}
    FILE
        swmm5-config.cmake
)

install(
    FILES
        ${SWMM_PUBLIC_HEADERS}
    DESTINATION
        ${CMAKE_INSTALL_INCLUDEDIR}
)

# Install OpenMP libraries
if(OpenMP_C_FOUND)
    install(
        FILES
            ${OpenMP_C_LIBRARIES}
        DESTINATION
            ${CMAKE_INSTALL_LIBDIR}
    )
endif()

# copy swmm5 to build tree for testing
add_custom_command(TARGET swmm5 POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy
        $<TARGET_FILE:swmm5>
        ${CMAKE_BINARY_DIR}/bin/$<CONFIGURATION>/$<TARGET_FILE_NAME:swmm5>
)
